locals {
  private_source_cidrs = length(var.private_subnet_cidrs) > 0 ? var.private_subnet_cidrs : compact([var.private_subnet_cidr])
}

resource "aws_security_group" "nat" {
  name        = "${var.name_prefix}-nat-sg"
  description = "NAT/jump host SG: SSH only from your IP; allow forwarding traffic from private subnet."
  vpc_id      = var.vpc_id

  ingress {
    description = "SSH from workstation"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = [var.allowed_ssh_cidr]
  }

  dynamic "ingress" {
    for_each = local.private_source_cidrs

    content {
      description = "Forwarded traffic from private subnet"
      from_port   = 0
      to_port     = 0
      protocol    = "-1"
      cidr_blocks = [ingress.value]
    }
  }

  egress {
    description = "All outbound"
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    "Name" = "${var.name_prefix}-nat-sg"
  })
}

resource "aws_instance" "nat" {
  ami                         = data.aws_ami.al2023.id
  instance_type               = var.instance_type
  subnet_id                   = var.public_subnet_id
  vpc_security_group_ids      = [aws_security_group.nat.id]
  key_name                    = var.key_name
  associate_public_ip_address = true

  source_dest_check = false

  iam_instance_profile = var.enable_ssm ? aws_iam_instance_profile.ssm[0].name : null

  user_data = templatefile("${path.module}/user_data.sh.tftpl", {
    private_subnet_cidrs = local.private_source_cidrs
  })
  user_data_replace_on_change = true

  metadata_options {
    http_endpoint               = "enabled"
    http_tokens                 = "required"
    http_put_response_hop_limit = 2
  }

  root_block_device {
    volume_type           = "gp3"
    encrypted             = true
    delete_on_termination = true
  }

  tags = merge(var.tags, {
    "Name" = "${var.name_prefix}-nat-instance"
    "Role" = "nat"
  })
}
